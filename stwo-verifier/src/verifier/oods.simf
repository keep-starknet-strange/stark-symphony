// SPDX-FileCopyrightText: 2025 StarkWare Industries Ltd.
//
// SPDX-License-Identifier: MIT

#ifndef VERIFIER_OODS_SIMF
#define VERIFIER_OODS_SIMF

#include "channel/state.simf"
#include "fields/qm31.simf"
#include "groups/qm31_point.simf"
#include "constraints/helpers.simf"
#include "verifier/proof.simf"

/// Reads the trace/cp polynomial commitments and evaluations at OODS, samples random CP coefficient and OODS point.
/// Fails if the computed CP evaluation does not match the sampled CP evaluation.
/// Returns the updated channel state.
fn oods_read_checked(state: ChannelState, log_size: u8, commitments: Commitments, oods_evals: OodsEvals) -> ChannelState {
    let (const_root, trace_root, cp_root): Commitments = commitments;
    let (oods_trace_evals, oods_cp_eval): OodsEvals = oods_evals;

    // Read preprocessed tree root (aka constant trace)
    let state: ChannelState = channel_mix_u256(state, const_root);
    // Read trace root
    let state: ChannelState = channel_mix_u256(state, trace_root);

    // Draw random coefficient for CP evaluation
    let (state, random_coeff): (ChannelState, QM31) = channel_draw_qm31(state);

    // Read composition polynomial commitment root
    let state: ChannelState = channel_mix_u256(state, cp_root);

    // Draw OODS point
    let (state, oods_point): (ChannelState, QM31Point) = channel_draw_qm31_point(state);

    // Read sampled values and CP partitioned evaluation
    let state: ChannelState = channel_mix_oods_evals(state, oods_evals);

    // Evaluate composition polynomial at OODS point
    let cp_eval: QM31 = eval_composition_poly(log_size, oods_point, oods_trace_evals, random_coeff);
    
    // Reconstruct sampled CP evaluation and compare
    let sampled_cp_eval: QM31 = composition_poly_eval_from_partitions(oods_cp_eval);
    assert!(qm31_eq(cp_eval, sampled_cp_eval));

    state
}

fn test_oods_read_checked() {
    let state: ChannelState = (0, 0);
    let log_size: u8 = 2;
    let commitments: Commitments = (
        0xe3b0c44298fc1c149afbf4c8996fb92427ae41e4649b934ca495991b7852b855,
        0x2151e980c7ae914e9b486e36b8392db505e1848bd7dcc89329245c61cdb95096,
        0xe85deb2eaadb5e5376ce81ffb6ee36eab838c2a97686f83d5de609ba03845e0c,
    );
    let oods_trace_evals: TraceEvalsQM31 = list![
        list![qm31(1, 0, 0, 0)],
        list![qm31(219216441, 1999484797, 865948224, 1842303184)],
        list![qm31(88796111, 196217285, 1337974825, 1079249662)],
        list![qm31(1039952504, 987562643, 1687835115, 1083917527)],
    ];
    let oods_cp_eval: CPEvalQM31 = [
        qm31(102457172, 120697777, 684798907, 704325503),
        qm31(1524382544, 864965792, 1090724300, 832226163),
        qm31(1447800586, 1051306930, 2035698572, 902029335),
        qm31(1674296998, 584689294, 411846336, 680031775),
    ];
    let oods_evals: OodsEvals = (oods_trace_evals, oods_cp_eval);
    let (digest, _): ChannelState = oods_read_checked(state, log_size, commitments, oods_evals);
    assert!(jet::eq_256(digest, 0x44dff4effae0be493c21672683c892b3baeaa7d251e5e7153bc0e0b2da8a9d96));
}

#endif
